plugins {
    id 'org.quiltmc.loom'
}

archivesBaseName = "${mod_id}-Quilt"

loom {
    runs {
        client {
            client()
            setConfigName("Quilt Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Quilt Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${mc_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${rootProject.parchmentMcVersion}:${parchment_version}@zip")
    }

    modImplementation "org.quiltmc:quilt-loader:${quilt_loader_version}"

	modImplementation ("org.quiltmc.quilted-fabric-api:quilted-fabric-api:${quilted_fabric_version}-${mc_version}") {
		exclude group: "org.quiltmc", module: "qsl"
	}
	modImplementation ("org.quiltmc:qsl:${qsl_version}+${mc_version}") {
		exclude group: "net.fabricmc"
	}

    implementation project(":Common")
}

idea {
    module {
        inheritOutputDirs = false
    }
}

processResources {
    from project(":Common").sourceSets.main.resources
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

java {
    withSourcesJar()
}

jar {
    from('LICENSE') {
        rename { "${it}_${archivesBaseName}" }
    }
}